Execute(setup):
  fu! Fixture(path, lines) abort
    if !isdirectory(fnamemodify(a:path, ':h'))
      call mkdir(fnamemodify(a:path, ':h'), 'p')
    endif
    noswap exe 'edit' fnameescape(a:path)
    %delete
    call setline(1, a:lines)
    write!
    return a:path
  endfu

  fu! CantWrite(path) abort
    let messages = split(execute('messages'), "\n")[-2:-1]
    return messages[0] ==# 'Can''t write changes to the following files:'
      \ && messages[1] =~# '^\^I'.fnamemodify(a:path, ':.')
  endfu

  fu! UndotreeIsConsistent() abort
    return (len(b:esearch.undotree.head.state) - 1 == line('$')
        \ || b:esearch.undotree.head.state == [0])
  endfu

  " linear congruential generator
  fu! Rand(from, to) abort 
    let s:next = (4096 * get(s:, 'next', 1) + 150889) % 714025
    return a:from + s:next % (a:to - a:from)
  endfu

  fu! Maparg(name, mode) abort
    let dict = maparg(a:name, a:mode, 0, 1)
    silent! call remove(dict, 'lnum')
    silent! call remove(dict, 'sid')
    return dict
  endfu

  let g:test_number = esearch#util#counter()
  let g:esearch_yes = 1
  let g:esearch = {
  \ 'backend': 'system',
  \ 'regex': 1,
  \ 'adapter': 'rg',
  \ 'win_new': {_-> esearch#buf#goto_or_open('[esearch-vader]', 'noswap tabnew')},
  \ 'adapters': {'rg': {'options': '--sort path'}},
  \ 'live_update': 0,
  \ 'root_markers': [],
  \}

  let g:mapleader = ","
  set nomore splitright splitbelow shortmess=lnTtoOfF
